BEGIN_OPTIONS
	Workflow "Unlit"
	Alpha "Blend"
END_OPTIONS

BEGIN_PASS("All")
	Cull Off
END_PASS

BEGIN_PROPERTIES
	_Color("Color", Color) = (1, 1, 1, 1)
	_MainTex("Main Tex", 2D) = "white" {}

	_NearTex("Near Tex", 2D) = "white" {}
	_NearScale("Near Scale", Float) = 1

	_DetailTex("Detail Tex", 2D) = "white" {}
	_DetailScale("Detail Scale", Vector) = (1,1,1)
	_DetailOffset("Detail Offset", Vector) = (0,0,0)
	_DetailTwist("Detail Twist", Float) = 0
	_DetailTwistBias("Detail Twist Bias", Float) = 0
END_PROPERTIES

BEGIN_DEFINES
	#pragma multi_compile_local __ _DETAIL
	#pragma multi_compile_local __ _NEAR
END_DEFINES

BEGIN_SUBSHADERS
	"../../Shared/Shaders/SGT_Matrix.surfshader"
	"../../Shared/Shaders/SGT_Output.surfshader"
END_SUBSHADERS

BEGIN_CBUFFER
	float4    _Color;
	sampler2D _MainTex;

	sampler2D _NearTex;
	float     _NearScale;

	sampler2D _DetailTex;
	float2    _DetailScale;
	float2    _DetailOffset;
	float     _DetailTwist;
	float     _DetailTwistBias;
END_CBUFFER

BEGIN_CODE
	void ModifyVertex(inout VertexData v, inout ExtraV2F e)
	{
		float4 wpos = SGT_O2W(v.vertex);
		float3 wcam = _WorldSpaceCameraPos;

		v.texcoord2.xyz = wpos.xyz - wcam;
	}

	void SurfaceFunction(inout Surface o, inout ShaderData d)
	{
		d.texcoord0.y = d.texcoord1.y / d.texcoord1.x;

		float4 finalColor = _Color * tex2D(_MainTex, d.texcoord0.xy);

		#if _DETAIL
			d.texcoord0.y += pow(d.texcoord0.x, _DetailTwistBias) * _DetailTwist;
			float4 detail = tex2D(_DetailTex, d.texcoord0 * _DetailScale + _DetailOffset);
			finalColor *= detail;
		#endif

		#if _NEAR // Near
			float2 near01 = length(d.texcoord2.xyz) * _NearScale;
			float  near = tex2D(_NearTex, near01).a;
			finalColor.a *= near;
		#endif

		finalColor.rgb *= finalColor.a;

		OutputWithAlpha(o, ModifyUnlitOutput(finalColor));
	}
END_CODE